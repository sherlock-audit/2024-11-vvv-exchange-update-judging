Shallow Pewter Bear

Medium

# `_isSignatureValid(InvestParams)` Vulnerable To Signature Replay Attack

### Summary

The current `signer`'s signatures can be permissionlessly replayed.

### Root Cause

A `nonce` is not enforced for the `InvestParams` signature, meaning a signature can be replayed for the lifetime of the signature:

```solidity
    /**
     * @notice Checks if the provided signature is valid
     * @param _params An InvestParams struct containing the investment parameters
     * @return true if the signer address is recovered from the signature, false otherwise
     */
    function _isSignatureValid(InvestParams memory _params) internal view returns (bool) {
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(
                    abi.encode(
                        INVESTMENT_TYPEHASH,
                        _params.investmentRound,
                        _params.investmentRoundLimit,
                        _params.investmentRoundStartTimestamp,
                        _params.investmentRoundEndTimestamp,
                        _params.paymentTokenAddress,
                        _params.kycAddress,
                        _params.kycAddressAllocation,
                        _params.exchangeRateNumerator,
                        _params.feeNumerator,
                        _params.deadline /// @audit Does not define a `params.nonce`.
                    )
                )
            )
        );

        address recoveredAddress = ECDSA.recover(digest, _params.signature);

        bool isSigner = recoveredAddress == signer;
        /// @audit Signature can be replayed until the deadline is met.
        bool isExpired = block.timestamp > _params.deadline;
        return isSigner && !isExpired;
    }
```

https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCInvestmentLedger.sol#L207C1-L240C6

Without using an auto-incrementing `nonce`, the signer's valid signature can be replayed repeatedly for the lifetime of the authorization of the signature.

This approval is used as the sole means access control about the `invest` function:

```solidity
    function invest(InvestParams memory _params) external {
        //check if investments are paused
        if (investmentIsPaused) revert InvestmentPaused();

        /// @audit `invest` is invoked permissionlessly provided there is a
        /// @audit valid signature object provided
        // check if signature is valid
        if (!_isSignatureValid(_params)) {
            revert InvalidSignature();
        }
```

https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCInvestmentLedger.sol#L141C5-L148C10

### Internal pre-conditions

1. Protocol is not paused.

### External pre-conditions

1. The `signer` produces a signature for some `InvestParams` and submits a transaction with the expectation it will only be executed once, and has sent the transaction. 
3. Chain has not split.

### Attack Path

1. Attacker monitoring the transaction can re-execute the same transaction object (they can either watch the mempool or copy the data after it has been mined).
2. The signature can be permissionlessly replayed.

### Impact

Signers can be griefed into executing a payload designed for single execution to be executed multiple times (i.e. make unexpected trades) by a permissionless actor. This could be used to exploit a signature that [secures an attractive interest rate](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCInvestmentLedger.sol#L164C9-L166C37).

### PoC

_No response_

### Mitigation

Manage a `nonce` on the `VVVVCInvestmentLedger` to ensure signatures cannot be replayed:

```diff
bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(
                    abi.encode(
                        INVESTMENT_TYPEHASH,
                        _params.investmentRound,
                        _params.investmentRoundLimit,
                        _params.investmentRoundStartTimestamp,
                        _params.investmentRoundEndTimestamp,
                        _params.paymentTokenAddress,
                        _params.kycAddress,
                        _params.kycAddressAllocation,
                        _params.exchangeRateNumerator,
                        _params.feeNumerator,
-                       _params.deadline
+                       _params.deadline,
+                       _nonce[signer]++
                    )
                )
            )
);
```