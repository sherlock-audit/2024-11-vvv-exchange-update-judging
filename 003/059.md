Bubbly Magenta Troll

Medium

# Wrong encoding of arrays in digest calculation violates `EIP-712`

## Summary
The `_isSignatureValid` function fails to correctly encode dynamic `array` fields (`projectTokenProxyWallets` and `tokenAmountsToClaim`) in the `ClaimParams` struct as per `EIP-712` standards. This leads to invalid digest generation, causing signature verification failures and breaking contract functionality relying on signature validation.

## Vulnerability Detail

In `VVVVCTokenDistributor.sol`, `ClaimParams` struct is defined as:

```js
struct ClaimParams {
        address kycAddress;
        address projectTokenAddress;
@>      address[] projectTokenProxyWallets;
@>      uint256[] tokenAmountsToClaim;
        uint256 nonce;
        uint256 deadline;
        bytes signature;
    }
```
While calclulating the `digest` in the [_isSignatureValid](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L157) function, encoding is done incorrectly for dynamic arrays: 
```js
 function _isSignatureValid(ClaimParams memory _params) private view returns (bool) {
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(
                    abi.encode(
                        CLAIM_TYPEHASH,
                        _params.kycAddress,
                        _params.projectTokenAddress,
@>                      _params.projectTokenProxyWallets,
@>                      _params.tokenAmountsToClaim,
                        _params.nonce,
                        _params.deadline
                    )
                )
            )
        );

        address recoveredAddress = ECDSA.recover(digest, _params.signature);

        bool isSigner = recoveredAddress == signer;
        bool isExpired = block.timestamp > _params.deadline;
        return isSigner && !isExpired;
    }
```
Here, the arrays `_params.projectTokenProxyWallets` and `_params.tokenAmountsToClaim` are directly included in `abi.encode` without being hashed.This violates the `EIP-712` standard and produces an invalid struct hash. 

[EIP-712](https://eips.ethereum.org/EIPS/eip-712#definition-of-encodedata) requires that:
> The array values are encoded as the keccak256 hash of the concatenated encodeData of their contents

## Impact

The generated digest does not match the expected value, causing signature verification to fail.

## Recommendation
```diff
function _isSignatureValid(ClaimParams memory _params) private view returns (bool) {
    bytes32 projectTokenProxyWalletsHash = keccak256(abi.encodePacked(_params.projectTokenProxyWallets));
    bytes32 tokenAmountsToClaimHash = keccak256(abi.encodePacked(_params.tokenAmountsToClaim));

    bytes32 structHash = keccak256(
        abi.encode(
            CLAIM_TYPEHASH,
            _params.kycAddress,
            _params.projectTokenAddress,
-           _params.projectTokenProxyWallets,
-           _params.tokenAmountsToClaim,
+           keccak256(abi.encodePacked(_params.projectTokenProxyWallets)),
+           keccak256(abi.encodePacked(_params.tokenAmountsToClaim)),
            _params.nonce,
            _params.deadline
        )
    );

    bytes32 digest = keccak256(abi.encodePacked("\x19\x01", DOMAIN_SEPARATOR, structHash));

    address recoveredAddress = ECDSA.recover(digest, _params.signature);

    bool isSigner = recoveredAddress == signer;
    bool isExpired = block.timestamp > _params.deadline;
    return isSigner && !isExpired;
}
```
