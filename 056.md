Raspy Hotpink Rook

High

# Malicious user/signer can frontrun investor's claim in `VVVVCTokenDistributor::claim`, causing loss of funds to the investor

### Summary

`VVVVCTokenDistributor::claim` sends `projectToken` to `msg.sender`, which will cause a loss of funds to the investor (the rightful owner of projectToken claim) as malicious user/signer will frontrun the investor's claim to obtain the `projectToken` before the investor.

In more detail, the intended functionality of `VVVVCTokenDistributor::claim` is to distribute `projectToken` to the rightful investors. However, the current implementation of `VVVVCTokenDistributor::claim` distributes `projectToken` to ANYONE (`msg.sender`) who can produce a valid `VVVVCTokenDistributor::ClaimParams` struct with a valid signature. Hence, anyone (malicious user or malicious signer) can frontrun the investor in claiming the `projectToken`, causing a loss of funds to the investor.

### Root Cause

In [VVVVCTokenDistributor.sol#L133](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L133), the function `VVVVCTokenDistributor::claim` sends `projectToken` to any `msg.sender` which calls the function as long as a valid `VVVVCTokenDistributor::ClaimParams` struct with a valid signature is passed as function arguments. 

### Internal pre-conditions

1. Signer must have created a valid `VVVVCTokenDistributor::ClaimParams` struct with a valid signature
2. Investor must have called `VVVVCTokenDistributor::claim` to claim their rightful `projectToken` (only required for the malicious user attack path)

### External pre-conditions

None

### Attack Path

There are 2 similar attack paths (malicious user and malicious signer) that exploit the same vulnerability, detailed below.

Malicious user attack path
1. Signer creates a valid `VVVVCTokenDistributor::ClaimParams` struct with a valid signature
2. Investor calls `VVVVCTokenDistributor::claim` using the `VVVVCTokenDistributor::ClaimParams` to claim their rightful projectToken
3. Malicious user sees the function call `VVVVCTokenDistributor::claim` and function arguments `VVVVCTokenDistributor::ClaimParams` in the mempool
4. Malicious user frontruns the investor by calling function call `VVVVCTokenDistributor::claim` and function arguments `VVVVCTokenDistributor::ClaimParams` using their (malicious user) account but with a higher priority gas fee
5. Malicious user claims the `projectToken` since `VVVVCTokenDistributor::claim` disburses `projectToken` to `msg.sender`
6. Investor `VVVVCTokenDistributor::claim` executes but reverts with `InvalidNonce` error
7. Investor is unable to claim their rightful `projectToken`, thus experiencing loss of funds

Malicious signer attack path
1. Signer creates a valid `VVVVCTokenDistributor::ClaimParams` struct with a valid signature
2. Signer calls `VVVVCTokenDistributor::claim` using the `VVVVCTokenDistributor::ClaimParams`
3. Signer claims the `projectToken` since `VVVVCTokenDistributor::claim` disburses `projectToken` to `msg.sender`
4. Investor is unable to claim their rightful `projectToken`, thus experiencing loss of funds

### Impact

Impact: HIGH. The investor loses out on ALL their deserved `projectToken`. The attacker (malicious user/signer) gains ALL of the `projectToken`.
Likelihood: HIGH. An attacker will always be able to frontrun the investor's `VVVVCTokenDistributor::claim` function call and has an incentive to do so.
Severity: HIGH

### PoC

Place the following code into `VVVVCTokenDistributor.unit.t.sol` and run
> forge test --mt testFrontrunClaim

```javascript
    function testFrontrunClaim() public {
        address[] memory thisProjectTokenProxyWallets = new address[](1);
        uint256[] memory thisTokenAmountsToClaim = new uint256[](1);

        thisProjectTokenProxyWallets[0] = projectTokenProxyWallets[0];

        uint256 claimAmount = sampleTokenAmountsToClaim[0];
        thisTokenAmountsToClaim[0] = claimAmount;

        // Step 1: Signer creates a valid `VVVVCTokenDistributor::ClaimParams` struct with a valid signature
        VVVVCTokenDistributor.ClaimParams memory claimParams = generateClaimParamsWithSignature(
            sampleKycAddress,
            thisProjectTokenProxyWallets,
            thisTokenAmountsToClaim
        );

        // Step 2: Malicious user/signer frontruns the investor's claim
        address mAlice = makeAddr("mAlice");
        claimAsUser(mAlice, claimParams);
        assertTrue(ProjectTokenInstance.balanceOf(mAlice) == claimAmount);

        // Step 3: Investor's claim fails as the nonce was consumed
        vm.expectRevert(VVVVCTokenDistributor.InvalidNonce.selector);
        claimAsUser(sampleKycAddress, claimParams);
    }
```

### Mitigation

As the intended functionality of `VVVVCTokenDistributor::claim` is to distribute `projectToken` to the rightful investors, `_params.kycAddress` should be used instead of `msg.sender` (see below). By applying this code change, the impact of frontrunning is mitigated since anyone calling `VVVVCTokenDistributor::claim` with valid `VVVVCTokenDistributor::ClaimParams` struct with a valid signature would still distribute `projectToken` to the rightful investor (`_params.kycAddress`) as shown in the PoC below.

Code Changes
```diff
function claim(ClaimParams memory _params) public {
        if (claimIsPaused) {
            revert ClaimIsPaused();
        }

        if (_params.projectTokenProxyWallets.length != _params.tokenAmountsToClaim.length) {
            revert ArrayLengthMismatch();
        }

        if (_params.nonce <= nonces[_params.kycAddress]) {
            revert InvalidNonce();
        }

        if (!_isSignatureValid(_params)) {
            revert InvalidSignature();
        }

        // update nonce
        nonces[_params.kycAddress] = _params.nonce;

        // define token to transfer
        IERC20 projectToken = IERC20(_params.projectTokenAddress);

        // transfer tokens from each wallet to the caller
        for (uint256 i = 0; i < _params.projectTokenProxyWallets.length; i++) {
            projectToken.safeTransferFrom(
                _params.projectTokenProxyWallets[i],
-               msg.sender,
+               _params.kycAddress,
                _params.tokenAmountsToClaim[i]
            );
        }

        emit VCClaim(
            _params.kycAddress,
            _params.projectTokenAddress,
            _params.projectTokenProxyWallets,
            _params.tokenAmountsToClaim,
            _params.nonce
        );
    }
```

Below is the PoC showing that frontrunning is mitigated with the above code changes.
After making the above code changes, place the following code into `VVVVCTokenDistributor.unit.t.sol` and run
> forge test --mt testFrontrunClaimFixed

```javascript
function testFrontrunClaimFixed() public {
        address[] memory thisProjectTokenProxyWallets = new address[](1);
        uint256[] memory thisTokenAmountsToClaim = new uint256[](1);

        thisProjectTokenProxyWallets[0] = projectTokenProxyWallets[0];

        uint256 claimAmount = sampleTokenAmountsToClaim[0];
        thisTokenAmountsToClaim[0] = claimAmount;

        VVVVCTokenDistributor.ClaimParams memory claimParams = generateClaimParamsWithSignature(
            sampleKycAddress,
            thisProjectTokenProxyWallets,
            thisTokenAmountsToClaim
        );

        // malicious user/signer attempts to frontrun an investor's claim
        // but the frontrun has no effect as investor's still receives their project token
        address mAlice = makeAddr("mAlice");
        claimAsUser(mAlice, claimParams);
        assertTrue(ProjectTokenInstance.balanceOf(sampleKycAddress) == claimAmount);
        assertFalse(ProjectTokenInstance.balanceOf(mAlice) == claimAmount);
    }
```