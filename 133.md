Vast Plastic Lemur

High

# `VVVVCTokenDistributor.sol`: Attacker can steal claims and DoS `claim()` transactions

### Summary

The [claim function](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L106-L145) in `VVVVCTokenDistributor.sol` is vulnerable to frontrunning attacks. An attacker can observe a `claim` transaction in the mempool, replicate it with a higher gas fee,  and execute it before the original transaction, effectively stealing the claim tokens and preventing  the legitimate claimant from claiming due to `nonce` reuse.
```javascript
    function claim(ClaimParams memory _params) public {
        if (claimIsPaused) {
            revert ClaimIsPaused();
        }


        if (_params.projectTokenProxyWallets.length != _params.tokenAmountsToClaim.length) {
            revert ArrayLengthMismatch();
        }


        if (_params.nonce <= nonces[_params.kycAddress]) {
            revert InvalidNonce();
        }


        if (!_isSignatureValid(_params)) {
            revert InvalidSignature();
        }


        // update nonce
        nonces[_params.kycAddress] = _params.nonce;


        // define token to transfer
        IERC20 projectToken = IERC20(_params.projectTokenAddress);


        // transfer tokens from each wallet to the caller
        for (uint256 i = 0; i < _params.projectTokenProxyWallets.length; i++) {
            projectToken.safeTransferFrom(
                _params.projectTokenProxyWallets[i],
                msg.sender,
                _params.tokenAmountsToClaim[i]
            );
        }


        emit VCClaim(
            _params.kycAddress,
            _params.projectTokenAddress,
            _params.projectTokenProxyWallets,
            _params.tokenAmountsToClaim,
            _params.nonce
        );
    }
```

### Root Cause

- The vulnerability stems from the [lack of binding between the `msg.sender` and the signed parameters (`_params`)](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L157-L181). 
- The function checks for a valid nonce and signature but does not validate if the `msg.sender` is an address 
authorized (by the signature), thereby allowing anyone to claim tokens by front-running.


### Internal pre-conditions

- `claimIsPaused` must be `false`.
- The `nonce` in `_params` must be greater than the last used nonce for the `_params.kycAddress`.
- The signature in `_params` must be valid and not expired.

### External pre-conditions

- The attacker observes pending transactions in the mempool.
- The attacker have enough gas to outbid the original transaction.

### Attack Path

* An attacker monitors the mempool for `claim` transactions.
* Upon spotting a `claim` transaction, the attacker copies the transaction data.
* The attacker frontruns this transaction with a higher gas fee, causing their transaction to be mined first.
* The attacker claims the tokens, and the original transaction reverts due to the `nonce` being used.


### Impact

- **Loss of Funds:** The legitimate claimant loses the claim tokens
- **Denial of Service (DoS):** The original claimant's transaction fails due to `nonce` reuse (prevents claiming at that `nonce` value)


### PoC

N/A

### Mitigation

1. **Sender Verification:**
   - Modify the `_isSignatureValid` function to include the sender's address in the verification:
     ```javascript
     function _isSignatureValid(ClaimParams memory _params) private view returns (bool) {
         bytes32 digest = keccak256(
             abi.encodePacked(
                 "\x19\x01",
                 DOMAIN_SEPARATOR,
                 keccak256(
                     abi.encode(
                         CLAIM_TYPEHASH,
                         _params.kycAddress,
                         _params.projectTokenAddress,
                         _params.projectTokenProxyWallets,
                         _params.tokenAmountsToClaim,
                         _params.nonce,
                         _params.deadline,
                         _params.sender  // Include msg.sender in the hash
                     )
                 )
             )
         );
         address recoveredAddress = ECDSA.recover(digest, _params.signature);
         bool isSigner = recoveredAddress == signer;
         bool isExpired = block.timestamp > _params.deadline;
         bool isSender = msg.sender == _params.sender;
         return isSigner && !isExpired && isSender;
     }
     ```
   - This ensures that only the intended recipient can claim the tokens.

OR

2. **Only KYC Address Can Claim Check:**
   - Add a check in the `claim` function to ensure that only the KYC address can claim:
     ```javascript
     if (_params.kycAddress != msg.sender) {
            revert UnauthorizedClaimer();
            }
     ```
