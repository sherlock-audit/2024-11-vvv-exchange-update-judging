Modern Wooden Rabbit

High

# Incorrect Encoding of Arrays in EIP712 Signatures Leading to Mismatched Signature Validation

### Summary

The signature in `VVVVCTokenDistributor._isSignatureValid()` is derived from a digest computed using `ClaimParams`. However, the `ClaimParams` structure includes an array argument that is not properly encoded as per the EIP712 specification. As a result, signatures generated using standard EIP712 tools fail validation, causing the `claim` function to revert with an InvalidSignature error.

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

According to [EIP712](https://eips.ethereum.org/EIPS/eip-712#definition-of-encodedata)

> The array values are encoded as the keccak256 hash of the concatenated encodeData of their contents (i.e. the encoding of SomeType[5] is identical to that of a struct containing five members of type SomeType).

https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/main/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L157

However, the `_isSignatureValid()` function in the `VVVCTokenDistributor.sol` contract reconstructs the digest by encoding the `ï»¿_params.projectTokenProxyWallets `and `_params.tokenAmountsToClaim` array arguments without first applying keccak256 hashing. As a result, a signature generated using common EIP712 tools (e.g. using the signTypedData function from ethers.js) would not pass validation in `ï»¿VVVCTokenDistributor.isSignatureValid()`.

### PoC

1. Compute the EIP712 signature using the ethersJs

```javascript
const { ethers, parseUnits } = require('ethers');

async function main() {
  const pk = '0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80';
  const signer = new ethers.Wallet(pk);
  const domain = {
    name: 'VVVdevelopment',
    chainId: 31337,
    verifyingContract: '0xE82f6BD9565612CE7A960f5e0855BE68f9fEB58c', // should match the address in foundry test
  };
  const types = {
    ClaimParams: [
      { name: 'kycAddress', type: 'address' },
      { name: 'projectTokenAddress', type: 'address' },
      { name: 'projectTokenProxyWallets', type: 'address[]' },
      { name: 'tokenAmountsToClaim', type: 'uint256[]' },
      { name: 'nonce', type: 'uint256' },
      { name: 'deadline', type: 'uint256' },
    ],
  };
  // All the details below should match in foundry test
  const value = {
    kycAddress: '0x059F6b2a6620c9E07aec6Cca364B7110F8029db9',
    projectTokenAddress: '0x04a19Aaae62Aa4603aB4240E92D2639AC90999eA',
    projectTokenProxyWallets: [
      '0x109136A644e106eC22988c23c38bA83714991512',
      '0xbc7E05dC05d7678C0bEbAd9447C7b5932B16dA33',
      '0x12823FF4A49F052b5099DFd306BB1594B9F6A8f0',
    ],
    tokenAmountsToClaim: [parseUnits('1111', 18), parseUnits('2222', 18), parseUnits('3333', 18)],
    nonce: 1,
    deadline: 86401,
  };
  const signature = await signer.signTypedData(domain, types, value);
  console.log(signature);
}

main();
```

2. Install ethers and run the code
```bash
npm install ethers
node main.js
```

Output: `0x0a6398ca35230bee84786d330bfaaad72952e4492464e671c352dfa4fad76c0e6a3b1d6b372315c260e4f8ce1a33848f8f62d1f4dbe18689f1d0c0c3aa9af2bc1b`

3. Compute EIP712 signature using foundry

```solidity
function _isSignatureValid(ClaimParams memory _params) private view returns (bool) {
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(
                    abi.encode(
                        CLAIM_TYPEHASH,
                        _params.kycAddress,
                        _params.projectTokenAddress,
                        _params.projectTokenProxyWallets,
                        _params.tokenAmountsToClaim,
                        _params.nonce,
                        _params.deadline
                    )
                )
            )
        );

        address recoveredAddress = ECDSA.recover(digest, _params.signature);

        bool isSigner = recoveredAddress == signer;
        bool isExpired = block.timestamp > _params.deadline;
        return isSigner && !isExpired;
    }
```
```solidity
function getEIP712SignatureForClaim(
        bytes32 _domainSeparator,
        bytes32 _claimTypehash,
        VVVVCTokenDistributor.ClaimParams memory _params
    ) public view returns (bytes memory) {
        uint256 signer = 0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80;
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                _domainSeparator,
                keccak256(
                    abi.encode(
                        _claimTypehash,
                        _params.kycAddress,
                        _params.projectTokenAddress,
                        _params.projectTokenProxyWallets,
                        _params.tokenAmountsToClaim,
                        _params.nonce,
                        _params.deadline
                    )
                )
            )
        );

        (uint8 v, bytes32 r, bytes32 s) = vm.sign(signer, digest);
        bytes memory signature = toBytesConcat(r, s, v);
        console.logBytes(signature);

        return signature;
    }
```
Output: `0x03f1f0df49d8e77ab1ecf037726d31e78d7e5e1b4ffd720c33cde271caa7377b7f752b3d1e9b7c6db1f0e2e4fe2edf126edb57160ea8589c464820400ca1ab831c`

As you can see, both signatures are different, resulting in the `_isSignatureValid` function returning false and making the claim function revert and the user won't be able to claim tokens.

4.  Add the correct encoding 

```solidity
function _isSignatureValid(ClaimParams memory _params) private view returns (bool) {
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(
                    abi.encode(
                        CLAIM_TYPEHASH,
                        _params.kycAddress,
                        _params.projectTokenAddress,
                        keccak256(abi.encodePacked(_params.projectTokenProxyWallets)),
                        keccak256(abi.encodePacked(_params.tokenAmountsToClaim)),
                        _params.nonce,
                        _params.deadline
                    )
                )
            )
        );

        address recoveredAddress = ECDSA.recover(digest, _params.signature);
        bool isSigner = recoveredAddress == signer;
        bool isExpired = block.timestamp > _params.deadline;
        return isSigner && !isExpired;
    }
```

```solidity
function getEIP712SignatureForClaim(
        bytes32 _domainSeparator,
        bytes32 _claimTypehash,
        VVVVCTokenDistributor.ClaimParams memory _params
    ) public view returns (bytes memory) {
        uint256 signer = 0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80;
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                _domainSeparator,
                keccak256(
                    abi.encode(
                        _claimTypehash,
                        _params.kycAddress,
                        _params.projectTokenAddress,
                        keccak256(abi.encodePacked(_params.projectTokenProxyWallets)),
                        keccak256(abi.encodePacked(_params.tokenAmountsToClaim)),
                        _params.nonce,
                        _params.deadline
                    )
                )
            )
        );

        (uint8 v, bytes32 r, bytes32 s) = vm.sign(signer, digest);
        bytes memory signature = toBytesConcat(r, s, v);
        console.logBytes(signature);

        return signature;
    }
```
Output: `0x0a6398ca35230bee84786d330bfaaad72952e4492464e671c352dfa4fad76c0e6a3b1d6b372315c260e4f8ce1a33848f8f62d1f4dbe18689f1d0c0c3aa9af2bc1b`
 

As you can see now the signature matches the one from the ethersJs output

### Mitigation

Update the `_isSignatureValid()` function to hash array arguments as shown below.

```solidity
function _isSignatureValid(ClaimParams memory _params) private view returns (bool) {
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(
                    abi.encode(
                        CLAIM_TYPEHASH,
                        _params.kycAddress,
                        _params.projectTokenAddress,
                        keccak256(abi.encodePacked(_params.projectTokenProxyWallets)),
                        keccak256(abi.encodePacked(_params.tokenAmountsToClaim)),
                        _params.nonce,
                        _params.deadline
                    )
                )
            )
        );

        address recoveredAddress = ECDSA.recover(digest, _params.signature);
        bool isSigner = recoveredAddress == signer;
        bool isExpired = block.timestamp > _params.deadline;
        return isSigner && !isExpired;
    }
```