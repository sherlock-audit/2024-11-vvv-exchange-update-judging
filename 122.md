Sharp Ceramic Dolphin

Medium

# `addInvestmentRecords` function lacks critical checks that are found in the `invest` function allowing it to bypass limits and checks

### Summary

[`VVVVCInvestmentLedger.sol::addInvestmentRecords`](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCInvestmentLedger.sol#L256) function lacks critical checks that are found in the `invest` function such as if a round has ended or has not started, maximum investment amount for that round, a user's maximum investment amount for that round and if investments are not paused. Lack of these checks allow investments done through this function to bypass limits that are applied to investments. This is especially problematic as the authorized role that can pause the investments in the contract does not have to be the same address as the authorized role that can call `addInvestmentRecords` function. This can be observed in the `onlyAuthorized` modifier.
```solidity
    modifier onlyAuthorized() {
        if (!_registry.isAuthorized(address(this), msg.sig, msg.sender)) {
            revert UnauthorizedCaller();
        }
        _;
    }
```

### Root Cause

In the [`VVVVCInvestmentLedger.sol::256`](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCInvestmentLedger.sol#L256) `addInvestmentRecords` function, it is observed that
```solidity
    function addInvestmentRecords(
        address[] calldata _kycAddresses,
        uint256[] calldata _investmentRounds,
        uint256[] calldata _amountsToInvest
    ) external onlyAuthorized {
        if (
            _kycAddresses.length != _investmentRounds.length ||
            _investmentRounds.length != _amountsToInvest.length
        ) {
            revert ArrayLengthMismatch();
        }

        for (uint256 i = 0; i < _kycAddresses.length; i++) {
            address kycAddress = _kycAddresses[i];
            uint256 investmentRound = _investmentRounds[i];
            uint256 amountToInvest = _amountsToInvest[i];

            kycAddressInvestedPerRound[kycAddress][investmentRound] += amountToInvest;
            totalInvestedPerRound[investmentRound] += amountToInvest;
            emit VCInvestment(investmentRound, address(0), kycAddress, 0, 0, 0, amountToInvest);
        }
    }
```
there are no checks other than the checks for array length implemented, allowing the Authorized role for this function to invest for a user directly without any checks.
Taking a look at the `invest` function, we can observe some critical checks that are done for investments
```solidity
    function invest(InvestParams memory _params) external {
        //check if investments are paused
        if (investmentIsPaused) revert InvestmentPaused();

        // rest of the function

        // check if the investment round is active
        if (
            block.timestamp < _params.investmentRoundStartTimestamp ||
            block.timestamp > _params.investmentRoundEndTimestamp
        ) {
            revert InactiveInvestmentRound();
        }
        // rest of the function
        
        // check if kyc address has already invested the max stablecoin-equivalent amount for this round,
        // or if the total invested for this round has reached the limit
        if (
            postFeeStableAmountEquivalent > _params.kycAddressAllocation - kycAddressInvestedThisRound ||
            postFeeStableAmountEquivalent > _params.investmentRoundLimit - totalInvestedThisRound
        ) {
            revert ExceedsAllocation();
        }
       // rest of the function
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. A number of users has invested with the `invest` function during a round and filled up the maximum allowed amount they can invest and maximum amount allowed that can be invested in total in a round.
```solidity
    function invest(InvestParams memory _params) external {
```
2. Round has ended with these users investing
3. Authorized role for the `setInvestmentIsPaused` function has paused investments in the contract.
```solidity
    function setInvestmentIsPaused(bool _isPaused) external onlyAuthorized {
        investmentIsPaused = _isPaused;
    }
```
4. Authorized role for the `addInvestmentRecords` function invests on behalf of a user that has invested, for the round that has ended.
```solidity
    function addInvestmentRecords(
        address[] calldata _kycAddresses,
        uint256[] calldata _investmentRounds,
        uint256[] calldata _amountsToInvest
    ) external onlyAuthorized {
```
5. Function, lacking any checks, will update this users investments for a round that has ended, while this user has invested maximum amount they can, while the total amount that can be invested in that round has been reached and while the investments are paused in the contract
6. The checks that are applied to investments are successfully bypassed.

### Impact

1. Investments are possible when the investments are paused.
2. Maximum amount a user can invest in a round will bypass the limit that is set.
3. Maximum total amount that can be invested in a round will bypass the limit that is set.
4. Investments are possible for rounds that are ended or for rounds that have not yet started.

### PoC

_No response_

### Mitigation

Implement checks found in `invest` function to the `addInvestmentRecords` function. These checks are:
1. Are investments paused in the contract
2. Has the round started or not ended
3. Does the investment amount stay within the maximum total amount that can be invested in that round
4. Does the investment amount of user stay within the maximum amount this user can invest in that round

An example of these checks being implemented with the current contract functionality. Keep in mind it would be better and safer to save data such as `_investmentRoundStartTimestamps`, `_investmentRoundEndTimestamps`, `_kycAddressesAllocations` and `_investmentRoundLimits` in contract storage as state variables.
```solidity
    function addInvestmentRecords(
        address[] calldata _kycAddresses,
        uint256[] calldata _kycAddressesAllocations, // new input
        uint256[] calldata _investmentRounds,
        uint256 [] calldata _investmentRoundStartTimestamps, // new input
        uint256 [] calldata _investmentRoundEndTimestamps, // new input
        uint256[] calldata _amountsToInvest,
        uint256[] calldata _investmentRoundLimits // new input
    ) external onlyAuthorized {
        if (investmentIsPaused) revert InvestmentPaused(); // new check
        if (
            _kycAddresses.length != _investmentRounds.length ||
            _investmentRounds.length != _amountsToInvest.length
        ) {
            revert ArrayLengthMismatch();
        }

        for (uint256 i = 0; i < _kycAddresses.length; i++) {
            address kycAddress = _kycAddresses[i];
            uint256 investmentRound = _investmentRounds[i];
            uint256 amountToInvest = _amountsToInvest[i];
            uint256 kycAddressAllocation = _kycAddressesAllocations[i]; // new line
            uint256 investmentRoundStartTimestamp = _investmentRoundStartTimestamps[i]; // new line
            uint256 investmentRoundEndTimestamp = _investmentRoundEndTimestamps[i]; // new line
            uint256 investmentRoundLimit = _investmentRoundLimits[i]; // new line

            if ( // new check
            block.timestamp < _investmentRoundStartTimestamp || 
            block.timestamp > investmentRoundEndTimestamp
            ) {
            revert InactiveInvestmentRound();
            }

            if ( // new check
            amountToInvest > kycAddressAllocation - kycAddressInvestedPerRound[kycAddress][investmentRound] ||
            amountToInvest > investmentRoundLimit - totalInvestedPerRound[investmentRound]
        )   {
            revert ExceedsAllocation();
            }

            kycAddressInvestedPerRound[kycAddress][investmentRound] += amountToInvest;
            totalInvestedPerRound[investmentRound] += amountToInvest;
            emit VCInvestment(investmentRound, address(0), kycAddress, 0, 0, 0, amountToInvest);
        }
    }
```