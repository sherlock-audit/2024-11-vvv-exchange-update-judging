Vast Plastic Lemur

High

# `VVVVCInvestmentLedger.sol`: Fixed `exchangeRateDenominator` can cause griefing in `invest` function and loss of funds

### Summary

The `invest` function in `VVVVCInvestmentLedger.sol` is vulnerable to incorrect asset valuation due to a fixed `exchangeRateDenominator` set during deployment in `VVVVCInvestmentLedger.sol` [constructor](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCInvestmentLedger.sol#L124), which does not account for different token decimals across investment rounds which can lead to griefing and fund loss.
```javascript
    function invest(InvestParams memory _params) external {
        //check if investments are paused
        if (investmentIsPaused) revert InvestmentPaused();


        // check if signature is valid
        if (!_isSignatureValid(_params)) {
            revert InvalidSignature();
        }


        // check if the investment round is active
        if (
            block.timestamp < _params.investmentRoundStartTimestamp ||
            block.timestamp > _params.investmentRoundEndTimestamp
        ) {
            revert InactiveInvestmentRound();
        }


        // store kyc address and total amounts invested for this investment round
        uint256 kycAddressInvestedThisRound = kycAddressInvestedPerRound[_params.kycAddress][
            _params.investmentRound
        ];
        uint256 totalInvestedThisRound = totalInvestedPerRound[_params.investmentRound];


        // the stablecoin amount equivalent to the payment token amount supplied at the current exchange rate
        uint256 preFeeStableAmountEquivalent = (_params.amountToInvest * _params.exchangeRateNumerator) /
            exchangeRateDenominator;


        // the post-fee stableAmountEquivalent, to contribute toward user and round limits
        uint256 postFeeStableAmountEquivalent = preFeeStableAmountEquivalent -
            (preFeeStableAmountEquivalent * _params.feeNumerator) /
            FEE_DENOMINATOR;


        // check if kyc address has already invested the max stablecoin-equivalent amount for this round,
        // or if the total invested for this round has reached the limit
        if (
            postFeeStableAmountEquivalent > _params.kycAddressAllocation - kycAddressInvestedThisRound ||
            postFeeStableAmountEquivalent > _params.investmentRoundLimit - totalInvestedThisRound
        ) {
            revert ExceedsAllocation();
        }


        // update kyc address and total amounts invested for this investment round (in stablecoin terms)
        kycAddressInvestedPerRound[_params.kycAddress][
            _params.investmentRound
        ] += postFeeStableAmountEquivalent;
        totalInvestedPerRound[_params.investmentRound] += postFeeStableAmountEquivalent;


        // transfer tokens from msg.sender to this contract (in payment token terms)
        IERC20(_params.paymentTokenAddress).safeTransferFrom(
            msg.sender,
            address(this),
            _params.amountToInvest
        );


        // emit VCInvestment event (in stablecoin terms)
        emit VCInvestment(
            _params.investmentRound,
            _params.paymentTokenAddress,
            _params.kycAddress,
            _params.exchangeRateNumerator,
            exchangeRateDenominator,
            _params.feeNumerator,
            postFeeStableAmountEquivalent
        );
    }
```
Before we look at the root cause let's quickly understand how the equivalent amount of stablecoins (`preFeeStableAmountEquivalent`) based on the investment token amount is computed in the `invest` function:
```javascript
// the stablecoin amount equivalent to the payment token amount supplied at the current exchange rate
        uint256 preFeeStableAmountEquivalent = (_params.amountToInvest * _params.exchangeRateNumerator) /
            exchangeRateDenominator;
```
The goal is to calculate the stablecoin amount equivalent to the input payment token amount, using a fractional exchange rate provided as:
                                `Exchange Rate = exchangeRateNumerator / exchangeRateDenominator`
* `_params.amountToInvest`:
The amount of the investment token (e.g., `USDC`/`VVV`) you are converting.

* `_params.exchangeRateNumerator`:
The numerator of the exchange rate, typically representing the target stablecoin value per unit of the source token, adjusted for decimal differences (e.g. for `USDC` = `1 * 10e6`).

* `exchangeRateDenominator`:
The denominator of the exchange rate to normalize the fractional value.

Formula Logic:
* `_params.amountToInvest` * `_params.exchangeRateNumerator` calculates the scaled stablecoin amount (adjusted by the numerator).
* Dividing by `exchangeRateDenominator` normalizes the value back to the stablecoin's decimal representation.

Example: Suppose:
* `_params.amountToInvest` = `10 * 10^18` (10 `DAI` with 18 decimals),
* `_params.exchangeRateNumerator` = `1 * 10^6` (1 `USDC` with 6 decimals),
* `exchangeRateDenominator` = `1 * 10^18` (to normalize the 18 decimals of `DAI`).
* Therefore; `preFeeStableAmountEquivalent` = (`10 * 10e18` * `1 * 10e6`) / `1 * 10e18` = `10 USDC`.  This equals 10 `USDC`, represented in its native decimal format (6 decimals).

### Root Cause

- The `exchangeRateDenominator` is set at contract deployment, assuming all investment tokens have the same decimal precision, which is not always the case [(since investment tokens can be `vvv`, `usdc`, and `usdt` - all different decimal precisions)](https://discord.com/channels/812037309376495636/1306634803470139443/1306776695700852736).
- This leads to incorrect conversion of the investment amount to stablecoin value when tokens with different decimal places are used.
For example, if the set `exchangeRateDenominator` is `1 * 10e18` when the investment token is `VVV`/`DAI` (any 18 decimal tokens that will be used as investment token), if the investment token changes to `USDC` in the next round this will cause an incorrect value conversion [(because every round the `exchangeRateDenominator` should correspond to the investment token of the round in terms of decimal precision - but `exchangeRateDenominator` is fixed on deployment)](https://discord.com/channels/812037309376495636/1306634803470139443/1306879616941686845).


### Internal pre-conditions

- The `investmentIsPaused` flag must be `false`.
- The signature provided in `InvestParams` must be valid.
- The current timestamp must be within the active period of the investment round (i.e. has not passed `_params.deadline`).


### External pre-conditions

- Different investment tokens with varying decimal places (i.e. `vvv`, `usdc`, and `usdt`) are used in different rounds.

### Attack Path

**Incorrect Conversion:** 
- When a token with fewer decimals (like `USDC` with 6 decimals) is an investment token used, the conversion can result in an overly small stablecoin equivalent (assuming the set `exchangeRateDenominator` is `1 * 10e18`). 
Example with 1 `USDC`:
`preFeeStableAmountEquivalent = (1 * 10e6 * 1 * 10e6) / 1 * 10e18 = 0.00001 USDC` - (computes low overly small stablecoin equivalent i.e. loss of funds)

- When a token with more decimals is used, the conversion results in an overly large stablecoin equivalent (assuming the set `exchangeRateDenominator` is `1 * 10e6`).
Example with a token having 18 decimals (e.g. 1 `vvv`):
`preFeeStableAmountEquivalent = (1 * 10e18 * 1 * 10e6) / 1 * 10e6 = 1,000,000 USDC` - (computes overly large stablecoin equivalent which causes griefing in the `invest` function due to [`_params.investmentRoundLimit` and the `_params.kycAddressAllocation`](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/1791f41b310489aaa66de349ef1b9e4bd331f14b/vvv-platform-smart-contracts/contracts/vc/VVVVCInvestmentLedger.sol#L173-L180) - as these inputs will be represented in a proper stablecoin value with an accurate decimal value).

### Impact

- Users' investments are either undervalued or overvalued (if undervalued, loss of funds and if overvalued, causes griefing)
- **Fund Loss:** If the value is significantly undervalued, investors lose out on the real value of their investment.


### PoC

N/A

### Mitigation

- instead of using a fixed `exchangeRateDenominator`, implement a dynamic `exchangeRateDenominator` in the `invest` function based on the decimal places of the `paymentTokenAddress` for each round. This can be done by:
    ```javascript
    uint8 paymentTokenDecimals = IERC20Metadata(_params.paymentTokenAddress).decimals();
    uint256 exchangeRateDenominator  = 10**uint256(paymentTokenDecimals);
    
    uint256 preFeeStableAmountEquivalent = (_params.amountToInvest * _params.exchangeRateNumerator) / exchangeRateDenominator;
    ```
OR
- Implement a function to change the `exchangeRateDenominator` for each round.