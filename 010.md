Acidic Midnight Mustang

Medium

# `VVVVCTokenDistributor` is not EIP712 compliant as it does not encode arrays properly

### Summary
Within the `VVVVCTokenDistributor ` contract, the signer has to provide signatures which allow users to claim their funds.
As stated in the readme, the contract is expected to be EIP712 complaint 

> Q: Is the codebase expected to comply with any specific EIPs?
Both VVVVCInvestmentLedger.sol and VVVVCTokenDistributor.sol define EIP-712 domain components and utilize EIP-712 structured data formats in validating signature used to validate calls to VVVVCInvestmentLedger:invest() and VVVVCTokenDistributor:claim().

However, that's not the case 

```solidity
    function _isSignatureValid(ClaimParams memory _params) private view returns (bool) {
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(
                    abi.encode(
                        CLAIM_TYPEHASH,
                        _params.kycAddress,
                        _params.projectTokenAddress,
                        _params.projectTokenProxyWallets,
                        _params.tokenAmountsToClaim,
                        _params.nonce,
                        _params.deadline
                    )
                )
            )
        );

        address recoveredAddress = ECDSA.recover(digest, _params.signature);

        bool isSigner = recoveredAddress == signer;
        bool isExpired = block.timestamp > _params.deadline;
        return isSigner && !isExpired;
    }
```

All of `_params.projectTokenAddress`, ` _params.projectTokenProxyWallets` and ` _params.tokenAmountsToClaim`. As stated in EIP712, the array has to be encoded before hashing the data

> The array values are encoded as the keccak256 hash of the concatenated encodeData of their contents (i.e. the encoding of SomeType[5] is identical to that of a struct containing five members of type SomeType).


### Root Cause

Arrays are not encoded first before hashing 

### Affected Code 
https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/main/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L166

### Impact

Protocol does not follow EIP712, when it is expected to.

### Mitigation

```diff
    function _isSignatureValid(ClaimParams memory _params) private view returns (bool) {
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(
                    abi.encode(
                        CLAIM_TYPEHASH,
                        _params.kycAddress,
-                        _params.projectTokenAddress,
-                        _params.projectTokenProxyWallets,
-                        _params.tokenAmountsToClaim,
+                        keccak256(abi.encodePacked(_params.projectTokenAddress)),
+                        keccak256(abi.encodePacked(_params.projectTokenProxyWallets)),
+                       keccak256(abi.encodePacked(_params.tokenAmountsToClaim)),
                        _params.nonce,
                        _params.deadline
                    )
                )
            )
        );

        address recoveredAddress = ECDSA.recover(digest, _params.signature);

        bool isSigner = recoveredAddress == signer;
        bool isExpired = block.timestamp > _params.deadline;
        return isSigner && !isExpired;
    }
```


