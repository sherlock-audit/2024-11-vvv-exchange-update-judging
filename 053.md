Rapid Green Tarantula

Medium

# Incorrect encoding of array for EIP 712 digest in `_isSignatureValid()` causes signatures generated by common EIP 712 tools to be unusable

### Summary

The signature in `VVVVCTokenDistributor::_isSignatureValid()` is generated based on a digest computed from :

```solidity
bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(
                    abi.encode(
                        CLAIM_TYPEHASH,
                        _params.kycAddress,
                        _params.projectTokenAddress,
                        _params.projectTokenProxyWallets,   //@audit 
                        _params.tokenAmountsToClaim,        //@audit         
                        _params.nonce,
                        _params.deadline
                    )
                )
            )
        );
```

However, `projectTokenProxyWallets` and `tokenAmountsToClaim` are an array. Based on [EIP 712](https://eips.ethereum.org/EIPS/eip-712) :

> The array values are encoded as the `keccak256` hash of the concatenated `encodeData` of their contents (i.e. the encoding of `SomeType[5]` is identical to that of a struct containing five members of type `SomeType`).
> 

Consequently, besides not being in accordance with EIP 712 then a signature generated using common EIP 712 tools would not pass validation in `_isSignatureValid()`.

### Root Cause

*In [VVVVCTokenDistributor.sol:167 - 168](https://github.com/sherlock-audit/2024-11-vvv-exchange-update/blob/main/vvv-platform-smart-contracts/contracts/vc/VVVVCTokenDistributor.sol#L167-L168) array must be hashing first to `keccak256`*

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Not being in accordance with EIP 712 and a signature generated using common EIP 712 tools (e.g using the `signTypedData` function from `ethers.js` ) would not pass validation in `_isSignatureValid()`

### PoC

- Add this test code to `VVVVCTokenDistributorUnitTests.sol`
    
    ```solidity
    function testValidateSignatureShouldBe() public {
            VVVVCTokenDistributor.ClaimParams memory claimParams = generateClaimParamsWithSignatureShouldBe(
                sampleKycAddress,
                projectTokenProxyWallets,
                sampleTokenAmountsToClaim
            );
    
            assertFalse(TokenDistributorInstance.isSignatureValid(claimParams));
        }
    ```
    
- And add this test code to `VVVVCTestBase.sol`
    
    ```solidity
    function generateClaimParamsWithSignatureShouldBe(
            address _kycAddress,
            address[] memory _projectTokenProxyWallets,
            uint256[] memory _tokenAmountsToClaim
        ) public view returns (VVVVCTokenDistributor.ClaimParams memory) {
            VVVVCTokenDistributor.ClaimParams memory params = VVVVCTokenDistributor.ClaimParams({
                kycAddress: _kycAddress,
                projectTokenAddress: address(ProjectTokenInstance),
                projectTokenProxyWallets: _projectTokenProxyWallets,
                tokenAmountsToClaim: _tokenAmountsToClaim,
                nonce: TokenDistributorInstance.nonces(_kycAddress) + 1,
                deadline: block.timestamp + 1 hours,
                signature: bytes("placeholder")
            });
    
            bytes memory sig = getEIP712SignatureForClaimShouldBe(distributorDomainSeparator, claimTypehash, params);
    
            params.signature = sig;
    
            console.logBytes(sig);
    
            return params;
        }
        
         function getEIP712SignatureForClaimShouldBe(
            bytes32 _domainSeparator,
            bytes32 _claimTypehash,
            VVVVCTokenDistributor.ClaimParams memory _params
        ) public view returns (bytes memory) {
            bytes32 digest = keccak256(
                abi.encodePacked(
                    "\x19\x01",
                    _domainSeparator,
                    keccak256(
                        abi.encode(
                            _claimTypehash,
                            _params.kycAddress,
                            _params.projectTokenAddress,
                            keccak256(abi.encodePacked(_params.projectTokenProxyWallets)), 
                            keccak256(abi.encodePacked(_params.tokenAmountsToClaim)), 
                            _params.nonce,
                            _params.deadline
                        )
                    )
                )
            );
    
            (uint8 v, bytes32 r, bytes32 s) = vm.sign(testSignerKey, digest);
            bytes memory signature = toBytesConcat(r, s, v);
    
            return signature;
        }
    ```
    

Run `forge test --match-test testValidateSignature -vvv`

Result

```solidity
Ran 2 tests for test/vc/VVVVCTokenDistributor.unit.t.sol:VVVVCTokenDistributorUnitTests
[PASS] testValidateSignature() (gas: 68954)
Logs: // the result of signature without hashing the array first for generate digest
  0x6dbc7c56a41f90b3476a0d407526773d073304441a01ce5c1365103351e5c7011c0e674331ee08b1d59a6f4844e7aac47b31e98cbb7de680c58b27d7812fa4e61c

[PASS] testValidateSignatureShouldBe() (gas: 69132)
Logs: // the result of signature with hashing the array first for generate digest
  0x474037c76cc6c659c6ecaaaf850db044e46b0e33e023947e3ee9a7cdf01f290020a4555a697e8512688e1abae7655709cd9592d4be2985e82792df45018668f11b

Suite result: ok. 2 passed; 0 failed; 0 skipped; finished in 5.29ms (3.17ms CPU time)
```

### Mitigation

```solidity
bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(
                    abi.encode(
                        CLAIM_TYPEHASH,
                        _params.kycAddress,
                        _params.projectTokenAddress,
                        keccak256(abi.encodePacked(_params.projectTokenProxyWallets)),   //@audit 
                        keccak256(abi.encodePacked(_params.tokenAmountsToClaim)),        //@audit         
                        _params.nonce,
                        _params.deadline
                    )
                )
            )
        );
```